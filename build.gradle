plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.5.30'
    id "com.gradle.plugin-publish" version "0.16.0" // For publishing on https://plugins.gradle.org/ -> use publishPlugins task
    id 'maven-publish' // For publishing on local maven repository (for development purposes) -> use publishToMavenLocal task
}

repositories {
    mavenCentral()
}

group 'com.bugfender'
version '1.0.1'

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    compileOnly "com.android.tools.build:gradle:7.0.2"

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.30'
    implementation "com.squareup.okhttp3:okhttp:4.9.1"

    testImplementation "com.android.tools.build:gradle:7.0.2"
    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.9.1"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.0'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        uploadMappingPlugin {
            id = "com.bugfender.upload-mapping"
            implementationClass = "com.bugfender.UploadMappingPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/bugfender/gradle-mapping-upload'
    vcsUrl = 'https://github.com/bugfender/gradle-mapping-upload'
    description = 'Plugin to automatically upload ProGuard mapping files to Bugfender'
    tags = ['bugfender','mapping','logger','crash-report']

    plugins {
        greetingsPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Bugfender Android plugin'
        }
    }
}